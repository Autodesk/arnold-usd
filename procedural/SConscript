# vim: filetype=python
# Copyright 2019 Autodesk, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
## load our own python modules
from utils import system
from utils.build_tools import find_files_recursive, link_usd_libraries
from utils.dependencies import get_tbb_lib

import os

# import build env
Import('env')
local_env = env.Clone()

src_proc_dir  = os.path.join(local_env['ROOT_DIR'], 'procedural')
source_files = [os.path.join('main.cpp')]
src_reader_dir  = os.path.join(local_env['ROOT_DIR'], 'translator', 'reader')
src_writer_dir  = os.path.join(local_env['ROOT_DIR'], 'translator', 'writer')

# Compiler flags
if not system.IS_WINDOWS:
    local_env.Append(CXXFLAGS = Split('-fPIC -Wno-deprecated-register'))

if env['ARNOLD_HAS_SCENE_FORMAT_API']:
    local_env.Append(CPPDEFINES=['ARNOLD_HAS_SCENE_FORMAT_API'])

local_env.Append(CPPDEFINES=['USD_PROCEDURAL_NAME=%s' % env['USD_PROCEDURAL_NAME']])

# Include paths
includePaths = [
    '.',
    src_reader_dir,
    src_writer_dir
]
local_env.Append(CPPPATH = includePaths)

local_env.Append(LIBS = ['ai'])

usd_deps = []

if system.IS_LINUX:
    local_env.Append(LIBS = ['dl'])

if local_env['USD_BUILD_MODE'] == 'monolithic':
    usd_deps = [
        local_env['USD_MONOLITHIC_LIBRARY'],
        get_tbb_lib(env),
    ]
elif local_env['USD_BUILD_MODE'] == 'static':
    # static builds rely on a monolithic static library
    if system.IS_WINDOWS:
        usd_deps = [
            '-WHOLEARCHIVE:libusd_m', 
            'Ws2_32',
            'Dbghelp',
            'Shlwapi', 
            'advapi32' 
        ]
        if not local_env['TBB_STATIC']:
            usd_deps += [get_tbb_lib(env)]
    else:
        whole_archives = '%s/libusd_m.a' % local_env.subst(local_env['USD_LIB'])
        if local_env['TBB_STATIC']:
            whole_archives += ',%s/libtbb.a' % local_env.subst(local_env['TBB_LIB'])
        else:
            usd_deps = [get_tbb_lib(env)]

        if system.IS_LINUX:
            local_env.Append(LINKFLAGS=['-Wl,--whole-archive,%s,--no-whole-archive' % whole_archives])
            local_env.Append(LIBS = ['pthread'])
        else:
            local_env.Append(LINKFLAGS=['-Wl,-all_load,%s,-noall_load' % whole_archives])
else:  # shared libs
    usd_libs = [
        'sdf',
        'tf',
        'usd',
        'ar',
        'usdGeom',
        'usdShade',
        'usdUtils',
        'vt',
        'usdLux',
        'gf',
        'usdVol',
        'usdSkel',
        'usdRender',
        'work'
    ]

    usd_deps += [get_tbb_lib(env)]

    usd_libs, usd_sources = link_usd_libraries(local_env, usd_libs)
    usd_deps = usd_deps + usd_libs
    source_files = source_files + usd_sources

translatorLibPath = os.path.abspath(os.path.join(local_env['BUILD_BASE_DIR'], 'translator'))
local_env.Append(LIBPATH = [translatorLibPath])
local_env.Append(LIBS = ['usd_translator'])

local_env.Append(LIBS = usd_deps)
if local_env['USD_HAS_PYTHON_SUPPORT']:
    local_env.Append(LIBS = [local_env['PYTHON_LIB_NAME'], local_env['BOOST_LIB_NAME'] % 'python'])

if system.IS_LINUX:
    plugin_map = os.path.join(src_proc_dir, 'plugin.map')
    local_env.Append(LINKFLAGS = [ '-Wl,--version-script={}'.format(plugin_map) ])
    local_env.Append(LINKFLAGS = [ '-Wl,--exclude-libs=ALL' ])
    local_env.Append(CXXFLAGS = [ '-fvisibility=hidden' ])
elif system.IS_DARWIN:
    if local_env['ARNOLD_HAS_SCENE_FORMAT_API']:
        export_list = os.path.join(src_proc_dir, 'macos_export_list')
    else:
        export_list = os.path.join(src_proc_dir, 'macos_export_list_no_scene')
    local_env.Append(LINKFLAGS = Split('-Xlinker -S -Xlinker -x -Xlinker -exported_symbols_list -Xlinker {} '.format(export_list)))

# Build shared library for the Alembic procedural
USD = local_env.SharedLibrary('%s_proc' % local_env['USD_PROCEDURAL_NAME'], source_files, SHLIBPREFIX='')
Return('USD')
